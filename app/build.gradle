plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk AppConfig.compileSdkVersion
    defaultConfig {
        applicationId AppConfig.applicationId
        minSdk AppConfig.minSdkVersion
        targetSdk AppConfig.targetSdkVersion
        versionCode AppConfig.versionCode
        versionName AppConfig.versionName
        testInstrumentationRunner AppConfig.testRunner
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    renameAPK(applicationVariants, defaultConfig)

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures{
        viewBinding  true
    }
}

def renameAPK(applicationVariants, defaultConfig) {
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def formattedDate = new Date().format('dd-MM-yy')
            outputFileName = "App Scheduler" + "_" +  defaultConfig.versionName + "_" + defaultConfig.versionCode + "_" + formattedDate + "_" + variant.name + ".apk"
        }
    }
}


dependencies {

    implementation AppDep.coreKtx
    implementation AppDep.material
    implementation AppDep.appCompat
    implementation AppDep.fragment
    implementation AppDep.activity
    implementation AppDep.recyclerview
    implementation AppDep.cardView
    implementation AppDep.viewModel
    implementation AppDep.liveData
    implementation AppDep.constraintLayout
    implementation AppDep.preference
    implementation AppDep.lifecycle



    implementation AppDep.navigationUi
    implementation AppDep.navigationFragment

    implementation project(ModulesDep.common)
    implementation project(ModulesDep.assets)
    implementation project(ModulesDep.navigation)

    implementation project(ModulesDep.home)

    implementation AppDep.hiltAndroid
    kapt AppDep.hiltCompiler
    implementation AppDep.timber

    implementation AppDep.room
    implementation AppDep.roomRuntime
    kapt AppDep.roomCompiler

    testImplementation AppDep.junit
    androidTestImplementation AppDep.extJunit
    androidTestImplementation AppDep.espresso
}

kapt {
    correctErrorTypes = true
}